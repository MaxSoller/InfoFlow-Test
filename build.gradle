plugins {
  id 'java'
  id 'org.jastadd'
}

defaultTasks 'jar'

if (!file('IntraJ/extendj/jastadd_modules').exists()) {
  throw new GradleException('ExtendJ seems to be missing. Please run "git submodule init", then "git submodule update".')
}

jastadd {
  configureModuleBuild()

  modules {
    include("IntraJ/extendj/jastadd_modules") // Include core ExtendJ modules.
    include("jastadd_modules/intraj_modules") // Include core IntraJ modules.
    
    module "infoflow", { 
      imports "IntraJ" // IntraJ dependency for infoflow module.
      jastadd {
        basedir "src/jastadd/"
        include "**/*.ast"
        include "**/*.jadd"
        include "**/*.jrag"
      }
    }
  }

  // Target module to build:
  module = 'infoflow'
  jastaddOptions = [ "--rewrite=cnta",
                     "--safeLazy",
                     "--visitCheck=false",
                     "--optimize-imports"]
  astPackage = 'org.extendj.ast'
  parser.name = 'JavaParser'
  scanner.name = 'OriginalScanner'
}

sourceSets.main {
  java {
    srcDir 'IntraJ/extendj/src/frontend-main'
    srcDir 'IntraJ/src/java'
    srcDir 'src/java'
  }
  resources {
    srcDir 'IntraJ/src/java/magpiebridge/resources'
    srcDir 'IntraJ/extendj/src/res'
    srcDir jastadd.buildInfoDir
  }
  repositories {
    mavenLocal()
    flatDir { dirs "tools" }
  }
  dependencies {
    jastadd2 name: "jastadd2"
  }
}


dependencies{
    testImplementation 'junit:junit:4.12'
    implementation 'junit:junit:4.12'
    implementation files('tools/magpiebridge-0.1.6-SNAPSHOT-jar-with-dependencies.jar')
    implementation 'com.sun.net.httpserver:http:20070405'
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.29'
}

sourceSets.test{
  java{
    srcDir 'src/java/test/'
  }
}

test {
   useJUnit()


  dependsOn 'cleanTest'
}

task infoflow(type: Jar) { 
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from { configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) } } {
        exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
    }
    from sourceSets.main.output
    manifest {
        attributes 'Main-Class': 'org.extendj.IntraJ'
    }
    baseName = 'intraj'
    destinationDir= file('.')
}


artifacts {
  archives infoflow
}

//jar.manifest.attributes 'Main-Class': 'org.extendj.JavaCompiler'
jar.destinationDirectory = projectDir

// Java -source and -target version.
sourceCompatibility = targetCompatibility = '1.8'

task sourceZip(type: Zip) {
  description 'Builds a Zip file with the entire repisotory (including the IntraJ submodule).'
  destinationDirectory = projectDir
  archiveFileName = "infoflow-src.zip"

  from (projectDir) {
    exclude '**/.git'
    exclude '**/.gitignore'
    exclude '**/.gitattributes'
    exclude '**/.gitmodules'
    exclude 'build'
    exclude 'bin'
    exclude '.gradle'
    exclude '.classpath'
    exclude '.settings'
    exclude '.project'
    exclude '*.jar'
    exclude '*.zip'
    exclude '**/*.swp'
  }

  into 'infoflow'
}